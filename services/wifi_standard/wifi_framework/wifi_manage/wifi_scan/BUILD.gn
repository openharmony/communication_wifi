# Copyright (C) 2021-2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

if (defined(ohos_lite)) {
  import("//build/lite/config/component/lite_component.gni")
} else {
  import("//build/ohos.gni")
}

local_base_sources = [
  "../common/handler.cpp",
  "../common/internal_message.cpp",
  "../common/message_queue.cpp",
  "../common/state.cpp",
  "../common/state_machine.cpp",
  "//foundation/communication/wifi/services/wifi_standard/wifi_framework/common/log/log_helper.c",
  "scan_interface.cpp",
  "scan_monitor.cpp",
  "scan_service.cpp",
  "scan_state_machine.cpp",
]

local_base_include_dirs = [
  "//foundation/communication/wifi/interfaces/innerkits/native_cpp/wifi_standard/interfaces",
  "//foundation/communication/wifi/services/wifi_standard/ipc_framework/interface",
  "//foundation/communication/wifi/services/wifi_standard/ipc_framework/cRPC/include",
  "//foundation/communication/wifi/services/wifi_standard/wifi_framework/common/include",
  "//foundation/communication/wifi/services/wifi_standard/wifi_framework/common/config",
  "//foundation/communication/wifi/services/wifi_standard/wifi_framework/common/log",
  "//foundation/communication/wifi/services/wifi_standard/wifi_framework/common/utils",
  "//foundation/communication/wifi/services/wifi_standard/wifi_framework/common/net_helper",
  "//foundation/communication/wifi/services/wifi_standard/wifi_framework/wifi_manage/common",
  "//foundation/communication/wifi/services/wifi_standard/wifi_framework/wifi_manage/wifi_scan",
  "//foundation/communication/wifi/services/wifi_standard/wifi_framework/wifi_manage/",
  "//foundation/communication/wifi/services/wifi_standard/wifi_framework/wifi_manage/idl_client",
  "//foundation/communication/wifi/services/wifi_standard/wifi_framework/wifi_manage/idl_client/idl_interface",
  "//foundation/communication/wifi/services/wifi_standard/wifi_framework/wifi_manage/wifi_sta/net_conf",
  "//foundation/communication/wifi/services/wifi_standard/wifi_framework/wifi_manage/wifi_sta",
]

if (defined(ohos_lite)) {
  shared_library("wifi_scan_service") {
    sources = local_base_sources
    include_dirs = local_base_include_dirs
    include_dirs += [
      "//base/hiviewdfx/hilog_lite/interfaces/native/innerkits",
      "//third_party/bounds_checking_function/include",
    ]

    deps = [
      "//base/hiviewdfx/hilog_lite/frameworks/featured:hilog_shared",
      "//foundation/communication/wifi/services/wifi_standard/wifi_framework/wifi_manage:wifi_manager_service",
      "//foundation/communication/wifi/services/wifi_standard/wifi_framework/wifi_manage/idl_client:wifi_idl_client",
      "//third_party/bounds_checking_function:libsec_shared",
    ]

    defines = [ "OHOS_ARCH_LITE" ]
    configs -= [ "//build/lite/config:language_cpp" ]
    cflags_cc = [
      "-std=c++17",
      "-fno-rtti",
    ]
    ldflags = [
      "-fPIC",
      "-Wl,-E",
    ]
  }
} else {
  ohos_shared_library("wifi_scan_service") {
    install_enable = true
    sources = local_base_sources
    include_dirs = local_base_include_dirs
    include_dirs += [
      "//utils/native/base/include",
      "//base/hiviewdfx/hilog/interfaces/native/innerkits/include",
    ]

    deps = [
      "//foundation/communication/wifi/services/wifi_standard/wifi_framework/wifi_manage:wifi_manager_service",
      "//foundation/communication/wifi/services/wifi_standard/wifi_framework/wifi_manage/idl_client:wifi_idl_client",
      "//utils/native/base:utils",
    ]

    external_deps = [ "hiviewdfx_hilog_native:libhilog" ]
    cflags_cc = [
      "-std=c++17",
      "-fno-rtti",
    ]

    ldflags = [
      "-fPIC",
      "-Wl,-E",
    ]

    part_name = "wifi_standard"
    subsystem_name = "communication"
  }
}
