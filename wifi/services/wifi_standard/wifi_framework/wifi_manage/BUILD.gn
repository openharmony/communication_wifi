# Copyright (C) 2021-2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

if (defined(ohos_lite)) {
  import("//build/lite/config/component/lite_component.gni")
  import("//foundation/communication/wifi/wifi/wifi_lite.gni")
} else {
  import("//build/ohos.gni")
  import("//build/ohos/sa_profile/sa_profile.gni")
  import("//build/ohos_var.gni")
  import("//foundation/communication/wifi/wifi/wifi.gni")
}

################################################################################

if (defined(ohos_lite)) {
  shared_library("wifi_service_base") {
    sources = [
      "$WIFI_ROOT_DIR/services/wifi_standard/wifi_framework/wifi_manage/common/wifi_permission_helper.cpp",
      "$WIFI_ROOT_DIR/services/wifi_standard/wifi_framework/wifi_manage/common/wifi_permission_utils.cpp",
      "wifi_auth_center.cpp",
      "wifi_config_center.cpp",
      "wifi_device_callback_proxy_lite.cpp",
      "wifi_device_service_impl.cpp",
      "wifi_device_stub_lite.cpp",
      "wifi_dumper.cpp",
      "wifi_internal_event_dispatcher_lite.cpp",
      "wifi_manager.cpp",
      "wifi_protect.cpp",
      "wifi_protect_manager.cpp",
      "wifi_scan_callback_proxy_lite.cpp",
      "wifi_scan_service_impl.cpp",
      "wifi_scan_stub_lite.cpp",
      "wifi_service_manager.cpp",
    ]

    include_dirs = [
      "$WIFI_ROOT_DIR/frameworks/native/include",
      "$WIFI_ROOT_DIR/frameworks/native/interfaces",
      "$WIFI_ROOT_DIR/services/wifi_standard/ipc_framework/cRPC/include",
      "$WIFI_ROOT_DIR/services/wifi_standard/wifi_framework/wifi_toolkit/include",
      "$WIFI_ROOT_DIR/services/wifi_standard/wifi_framework/wifi_toolkit/log",
      "$WIFI_ROOT_DIR/services/wifi_standard/wifi_framework/wifi_toolkit/net_helper",
      "$WIFI_ROOT_DIR/services/wifi_standard/wifi_framework/wifi_toolkit/utils",
      "$WIFI_ROOT_DIR/services/wifi_standard/wifi_framework/wifi_toolkit/config",
      "$WIFI_ROOT_DIR/services/wifi_standard/wifi_framework/wifi_manage",
      "$WIFI_ROOT_DIR/services/wifi_standard/wifi_framework/wifi_manage/common",
      "$WIFI_ROOT_DIR/services/wifi_standard/wifi_framework/wifi_manage/idl_client",
      "$WIFI_ROOT_DIR/services/wifi_standard/wifi_framework/wifi_manage/idl_client/idl_interface",
      "$WIFI_ROOT_DIR/services/wifi_standard/wifi_framework/wifi_manage/wifi_sta",
      "$WIFI_ROOT_DIR/services/wifi_standard/wifi_framework/wifi_manage/wifi_scan",
      "$DHCP_ROOT_DIR/services/mgr_service/include",
      "$DHCP_ROOT_DIR/interfaces/inner_api/include",
      "$DHCP_ROOT_DIR/interfaces/inner_api/interfaces",
      "$WIFI_ROOT_DIR/utils/inc",
      "//base/hiviewdfx/hilog_lite/interfaces/native/innerkits",
      "//foundation/communication/ipc/interfaces/innerkits/c/ipc/include",
      "//foundation/communication/netmanager_base/services/netmanagernative/include/netsys",
      "//foundation/systemabilitymgr/samgr_lite/interfaces/innerkits/registry",
      "//foundation/systemabilitymgr/samgr_lite/interfaces/innerkits/samgr",
      "//third_party/bounds_checking_function/include",
      "$WIFI_ROOT_DIR/services/wifi_standard/include",
    ]

    deps = [
      "$WIFI_ROOT_DIR/services/wifi_standard/wifi_framework/wifi_manage/idl_client:wifi_idl_client",
      "$WIFI_ROOT_DIR/services/wifi_standard/wifi_framework/wifi_toolkit:wifi_toolkit",
      "$WIFI_ROOT_DIR/utils:wifi_utils",
      "//base/hiviewdfx/hilog_lite/frameworks/featured:hilog_shared",
      "//foundation/communication/ipc/interfaces/innerkits/c/ipc:ipc_single",
      "//foundation/systemabilitymgr/samgr_lite/samgr:samgr",
      "//third_party/bounds_checking_function:libsec_shared",
    ]

    defines = [
      "OHOS_ARCH_LITE",
      "AP_INSTANCE_MAX_NUM=$wifi_feature_with_ap_num",
    ]

    if (wifi_feature_with_auth_disable) {
      defines += [ "PERMISSION_ALWAYS_GRANT" ]
    }

    cflags_cc = [
      "-std=c++17",
      "-fno-rtti",
    ]
  }

  executable("wifi_manager_service") {
    sources = [
      "wifi_device_feature_lite.cpp",
      "wifi_sa_service_lite.c",
      "wifi_scan_feature_lite.cpp",
      "wifi_service_main_lite.c",
    ]

    include_dirs = [
      "$WIFI_ROOT_DIR/frameworks/native/include",
      "$WIFI_ROOT_DIR/frameworks/native/interfaces",
      "$WIFI_ROOT_DIR/services/wifi_standard/ipc_framework/cRPC/include",
      "$WIFI_ROOT_DIR/services/wifi_standard/wifi_framework/wifi_toolkit/include",
      "$WIFI_ROOT_DIR/services/wifi_standard/wifi_framework/wifi_toolkit/log",
      "$WIFI_ROOT_DIR/services/wifi_standard/wifi_framework/wifi_toolkit/net_helper",
      "$WIFI_ROOT_DIR/services/wifi_standard/wifi_framework/wifi_toolkit/utils",
      "$WIFI_ROOT_DIR/services/wifi_standard/wifi_framework/wifi_toolkit/config",
      "$WIFI_ROOT_DIR/services/wifi_standard/wifi_framework/wifi_manage",
      "$WIFI_ROOT_DIR/services/wifi_standard/wifi_framework/wifi_manage/common",
      "$WIFI_ROOT_DIR/services/wifi_standard/wifi_framework/wifi_manage/idl_client",
      "$WIFI_ROOT_DIR/services/wifi_standard/wifi_framework/wifi_manage/idl_client/idl_interface",
      "$WIFI_ROOT_DIR/services/wifi_standard/wifi_framework/wifi_manage/wifi_sta",
      "$WIFI_ROOT_DIR/services/wifi_standard/wifi_framework/wifi_manage/wifi_scan",
      "$DHCP_ROOT_DIR/services/mgr_service/include",
      "$DHCP_ROOT_DIR/interfaces/inner_api/include",
      "$DHCP_ROOT_DIR/interfaces/inner_api/interfaces",
      "$WIFI_ROOT_DIR/utils/inc",
      "//base/hiviewdfx/hilog_lite/interfaces/native/innerkits",
      "//foundation/communication/ipc/interfaces/innerkits/c/ipc/include",
      "//foundation/systemabilitymgr/samgr_lite/interfaces/innerkits/registry",
      "//foundation/systemabilitymgr/samgr_lite/interfaces/innerkits/samgr",
      "//third_party/bounds_checking_function/include",
    ]

    deps = [
      "$WIFI_ROOT_DIR/services/wifi_standard/wifi_framework/wifi_manage:wifi_service_base",
      "$WIFI_ROOT_DIR/services/wifi_standard/wifi_framework/wifi_manage/idl_client:wifi_idl_client",
      "$WIFI_ROOT_DIR/utils:wifi_utils",
      "//base/hiviewdfx/hilog_lite/frameworks/featured:hilog_shared",
      "//foundation/communication/ipc/interfaces/innerkits/c/ipc:ipc_single",
      "//foundation/systemabilitymgr/samgr_lite/samgr:samgr",
      "//third_party/bounds_checking_function:libsec_shared",
    ]

    defines = [ "OHOS_ARCH_LITE" ]
    cflags_cc = [
      "-std=c++17",
      "-fno-rtti",
    ]
  }
} else {
  config("wifi_manager_service_header") {
    include_dirs = [
      "//commonlibrary/c_utils/base/include",
      "//base/hiviewdfx/hilog/interfaces/native/innerkits/include",
      "//foundation/arkui/ace_engine/frameworks/base/utils",
      "//foundation/arkui/ace_engine/frameworks",
      "//foundation/systemabilitymgr/safwk/interfaces/innerkits/safwk/",
      "$WIFI_ROOT_DIR/frameworks/native/interfaces",
      "$WIFI_ROOT_DIR/services/wifi_standard/ipc_framework/cRPC/include",
      "$WIFI_ROOT_DIR/services/wifi_standard/wifi_framework/wifi_toolkit/include",
      "$WIFI_ROOT_DIR/services/wifi_standard/wifi_framework/wifi_toolkit/log",
      "$WIFI_ROOT_DIR/services/wifi_standard/wifi_framework/wifi_toolkit/net_helper",
      "$WIFI_ROOT_DIR/services/wifi_standard/wifi_framework/wifi_toolkit/utils",
      "$WIFI_ROOT_DIR/services/wifi_standard/wifi_framework/wifi_toolkit/config",
      "$WIFI_ROOT_DIR/services/wifi_standard/wifi_framework/wifi_manage",
      "$WIFI_ROOT_DIR/services/wifi_standard/wifi_framework/wifi_manage/common",
      "$WIFI_ROOT_DIR/services/wifi_standard/wifi_framework/wifi_manage/idl_client",
      "$WIFI_ROOT_DIR/services/wifi_standard/wifi_framework/wifi_manage/idl_client/idl_interface",
      "$WIFI_ROOT_DIR/services/wifi_standard/wifi_framework/wifi_manage/wifi_sta",
      "$WIFI_ROOT_DIR/services/wifi_standard/wifi_framework/wifi_manage/wifi_scan",
      "$WIFI_ROOT_DIR/services/wifi_standard/wifi_framework/wifi_manage/wifi_ap",
      "$WIFI_ROOT_DIR/services/wifi_standard/wifi_framework/wifi_manage/wifi_p2p",
      "$WIFI_ROOT_DIR/services/wifi_standard/wifi_framework/wifi_manage/wifi_p2p/hid2d",
      "$DHCP_ROOT_DIR/services/mgr_service/include",
      "$DHCP_ROOT_DIR/interfaces/inner_api/include",
      "$DHCP_ROOT_DIR/interfaces/inner_api/interfaces",
      "$WIFI_ROOT_DIR/utils/inc",
      "//foundation/appexecfwk/adapter/interfaces/innerkits/appexecfwk_base/include",
      "//base/security/access_token/interfaces/innerkits/accesstoken/include",
      "//foundation/communication/netmanager_base/frameworks/native/netmanagernative",
      "//foundation/communication/netmanager_base/interfaces/innerkits/netmanagernative/include",
      "//foundation/communication/netmanager_base/services/netmanagernative/include/netsys",
      "$WIFI_ROOT_DIR/services/wifi_standard/include",
    ]
  }

  config("wifi_manager_service_config") {
    visibility = [ "//:*" ]
    include_dirs = [ ":wifi_fw_common_header" ]

    cflags = [
      "-std=c++17",
      "-fno-rtti",
    ]
    if (target_cpu == "arm") {
      cflags += [ "-DBINDER_IPC_32BIT" ]
    }

    if (product_name == "rk3568") {
      defines = [ "NON_SEPERATE_P2P" ]
    }
  }

  ohos_source_set("wifi_scan_service_impl") {
    part_name = "wifi"
    sources = [
      "wifi_scan_callback_proxy.cpp",
      "wifi_scan_death_recipient.cpp",
      "wifi_scan_service_impl.cpp",
      "wifi_scan_stub.cpp",
    ]
    configs = [
      ":wifi_manager_service_config",
      ":wifi_manager_service_header",
    ]

    external_deps = [ "ipc:ipc_core" ]
  }
  ohos_source_set("wifi_device_service_impl") {
    part_name = "wifi"
    sources = [
      "wifi_device_callback_proxy.cpp",
      "wifi_device_death_recipient.cpp",
      "wifi_device_service_impl.cpp",
      "wifi_device_stub.cpp",
      "wifi_protect.cpp",
      "wifi_protect_manager.cpp",
    ]
    configs = [
      ":wifi_manager_service_config",
      ":wifi_manager_service_header",
    ]

    defines = []
    if (wifi_feature_with_p2p) {
      defines += [ "FEATURE_P2P_SUPPORT" ]
    }

    if (wifi_feature_with_ap_num > 0) {
      defines += [ "FEATURE_AP_SUPPORT" ]
    }

    deps = [ ":wifi_p2p_service_impl" ]
    external_deps = [
      "ability_base:want",
      "bundle_framework:appexecfwk_base",
      "c_utils:utils",
      "common_event_service:cesfwk_innerkits",
      "ipc:ipc_core",
    ]
  }
  ohos_source_set("wifi_hotspot_service_impl") {
    part_name = "wifi"
    sources = [
      "wifi_hotspot_callback_proxy.cpp",
      "wifi_hotspot_death_recipient.cpp",
      "wifi_hotspot_mgr_service_impl.cpp",
      "wifi_hotspot_mgr_stub.cpp",
      "wifi_hotspot_service_impl.cpp",
      "wifi_hotspot_stub.cpp",
    ]
    configs = [
      ":wifi_manager_service_config",
      ":wifi_manager_service_header",
    ]

    deps = [ "$WIFI_ROOT_DIR/services/wifi_standard/wifi_framework/wifi_toolkit:wifi_toolkit" ]

    external_deps = [ "ipc:ipc_core" ]
    defines = [
      "FEATURE_AP_SUPPORT",
      "AP_INSTANCE_MAX_NUM=$wifi_feature_with_ap_num",
    ]

    if (wifi_feature_with_ap_extension) {
      defines += [ "FEATURE_AP_EXTENSION" ]
    }
  }
  ohos_source_set("wifi_p2p_service_impl") {
    part_name = "wifi"
    sources = [
      "$WIFI_ROOT_DIR/frameworks/native/src/wifi_hid2d_msg.cpp",
      "$WIFI_ROOT_DIR/frameworks/native/src/wifi_p2p_msg.cpp",
      "wifi_p2p_callback_proxy.cpp",
      "wifi_p2p_death_recipient.cpp",
      "wifi_p2p_service_impl.cpp",
      "wifi_p2p_stub.cpp",
    ]
    configs = [
      ":wifi_manager_service_config",
      ":wifi_manager_service_header",
    ]

    deps = [ "$WIFI_ROOT_DIR/services/wifi_standard/wifi_framework/wifi_toolkit:wifi_toolkit" ]

    external_deps = [
      "ipc:ipc_core",
      "netmanager_base:net_conn_manager_if",
    ]
    defines = [ "FEATURE_P2P_SUPPORT" ]
  }

  ohos_shared_library("wifi_manager_service") {
    install_enable = true
    sources = [
      "$WIFI_ROOT_DIR/services/wifi_standard/wifi_framework/wifi_manage/common/wifi_permission_helper.cpp",
      "$WIFI_ROOT_DIR/services/wifi_standard/wifi_framework/wifi_manage/common/wifi_permission_utils.cpp",
      "wifi_auth_center.cpp",
      "wifi_config_center.cpp",
      "wifi_dumper.cpp",
      "wifi_internal_event_dispatcher.cpp",
      "wifi_manager.cpp",
      "wifi_net_agent.cpp",
      "wifi_service_manager.cpp",
    ]

    deps = [
      "$WIFI_ROOT_DIR/services/wifi_standard/etc/init:etc",
      "$WIFI_ROOT_DIR/services/wifi_standard/wifi_framework/wifi_manage/idl_client:wifi_idl_client",
      "$WIFI_ROOT_DIR/services/wifi_standard/wifi_framework/wifi_toolkit:wifi_toolkit",
      "$WIFI_ROOT_DIR/utils:wifi_utils",
      "//base/hiviewdfx/hilog/interfaces/native/innerkits:libhilog",
      "//foundation/communication/netmanager_base/services/netmanagernative:netsys_native_manager",
      "//foundation/systemabilitymgr/safwk/interfaces/innerkits/safwk:system_ability_fwk",
    ]

    configs = [ ":wifi_manager_service_header" ]

    external_deps = [
      "access_token:libaccesstoken_sdk",
      "c_utils:utils",
      "ipc:ipc_core",
      "netmanager_base:net_conn_manager_if",
      "samgr:samgr_proxy",
    ]

    defines = [
      "FEATURE_AP_SUPPORT",
      "FEATURE_P2P_SUPPORT",
      "AP_INSTANCE_MAX_NUM=$wifi_feature_with_ap_num",
    ]

    if (wifi_feature_with_app_frozen) {
      external_deps += [ "efficiency_manager:suspend_manager_client" ]
      defines += [ "FEATURE_APP_FROZEN" ]
    }

    if (wifi_feature_with_auth_disable) {
      defines += [ "PERMISSION_ALWAYS_GRANT" ]
    }

    if (wifi_feature_with_ap_extension) {
      defines += [ "FEATURE_AP_EXTENSION" ]
    }

    part_name = "wifi"
    subsystem_name = "communication"
  }

  ohos_shared_library("wifi_device_ability") {
    install_enable = true
    deps = [
      ":wifi_device_service_impl",
      ":wifi_manager_service",
      "$WIFI_ROOT_DIR/services/wifi_standard/wifi_framework/wifi_manage/idl_client:wifi_idl_client",
      "$WIFI_ROOT_DIR/utils:wifi_utils",
      "//base/hiviewdfx/hilog/interfaces/native/innerkits:libhilog",
      "//foundation/systemabilitymgr/safwk/interfaces/innerkits/safwk:system_ability_fwk",
    ]

    external_deps = [
      "c_utils:utils",
      "ipc:ipc_core",
    ]
    part_name = "wifi"
    subsystem_name = "communication"
  }

  ohos_shared_library("wifi_scan_ability") {
    install_enable = true
    deps = [
      ":wifi_manager_service",
      ":wifi_scan_service_impl",
      "//base/hiviewdfx/hilog/interfaces/native/innerkits:libhilog",
      "//foundation/systemabilitymgr/safwk/interfaces/innerkits/safwk:system_ability_fwk",
    ]

    external_deps = [
      "c_utils:utils",
      "ipc:ipc_core",
    ]
    part_name = "wifi"
    subsystem_name = "communication"
  }

  ohos_shared_library("wifi_hotspot_ability") {
    install_enable = true
    deps = [
      ":wifi_hotspot_service_impl",
      ":wifi_manager_service",
      "$WIFI_ROOT_DIR/utils:wifi_utils",
      "//base/hiviewdfx/hilog/interfaces/native/innerkits:libhilog",
      "//foundation/systemabilitymgr/safwk/interfaces/innerkits/safwk:system_ability_fwk",
    ]

    external_deps = [
      "c_utils:utils",
      "ipc:ipc_core",
    ]

    part_name = "wifi"
    subsystem_name = "communication"
  }

  ohos_shared_library("wifi_p2p_ability") {
    install_enable = true
    deps = [
      ":wifi_manager_service",
      ":wifi_p2p_service_impl",
      "$WIFI_ROOT_DIR/utils:wifi_utils",
      "//base/hiviewdfx/hilog/interfaces/native/innerkits:libhilog",
      "//foundation/systemabilitymgr/safwk/interfaces/innerkits/safwk:system_ability_fwk",
    ]

    external_deps = [
      "c_utils:utils",
      "ipc:ipc_core",
    ]
    part_name = "wifi"
    subsystem_name = "communication"
  }
}
