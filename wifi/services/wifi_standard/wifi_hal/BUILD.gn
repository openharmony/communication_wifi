# Copyright (C) 2021-2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

if (defined(ohos_lite)) {
  import("//build/lite/config/component/lite_component.gni")
  import("//foundation/communication/wifi/wifi/wifi_lite.gni")
} else {
  import("//build/ohos.gni")
  import("//foundation/communication/wifi/wifi/wifi.gni")
}

local_base_sources = [
  "common/wifi_hal_common_func.c",
  "main.c",
  "wifi_hal_adapter.c",
  "wifi_hal_ap_interface.c",
  "wifi_hal_base_interface.c",
  "wifi_hal_callback.c",
  "wifi_hal_chba_interface.c",
  "wifi_hal_chip_interface.c",
  "wifi_hal_crpc_ap.c",
  "wifi_hal_crpc_base.c",
  "wifi_hal_crpc_chip.c",
  "wifi_hal_crpc_common.c",
  "wifi_hal_crpc_p2p.c",
  "wifi_hal_crpc_server.c",
  "wifi_hal_crpc_sta.c",
  "wifi_hal_crpc_supplicant.c",
  "wifi_hal_module/hostapd_hal/wifi_hostapd_hal.c",
  "wifi_hal_module/wpa_supplicant_hal/wifi_wpa_common.c",
  "wifi_hal_module/wpa_supplicant_hal/wifi_wpa_hal.c",
  "wifi_hal_module/wpa_supplicant_hal/wpa_p2p_hal/wifi_p2p_hal.c",
  "wifi_hal_module/wpa_supplicant_hal/wpa_sta_hal/wifi_common_hal.c",
  "wifi_hal_module/wpa_supplicant_hal/wpa_sta_hal/wifi_supplicant_hal.c",
  "wifi_hal_module_manage.c",
  "wifi_hal_p2p_interface.c",
  "wifi_hal_sta_interface.c",
  "wifi_hal_vendor_interface.c",
]

local_base_include_dirs = [
  "$WIFI_ROOT_DIR/base/state_machine/include",
  "$WIFI_ROOT_DIR/base/cRPC/include",
  "$WIFI_ROOT_DIR/interfaces/inner_api",
  "$WIFI_ROOT_DIR/frameworks/native/interfaces",
  "$WIFI_ROOT_DIR/services/wifi_standard/wifi_hal",
  "$WIFI_ROOT_DIR/services/wifi_standard/wifi_hal/common",
  "$WIFI_ROOT_DIR/services/wifi_standard/wifi_hal/wifi_hal_module/hostapd_hal",
  "$WIFI_ROOT_DIR/services/wifi_standard/wifi_hal/wifi_hal_module/wpa_supplicant_hal",
  "$WIFI_ROOT_DIR/services/wifi_standard/wifi_hal/wifi_hal_module/wpa_supplicant_hal/wpa_sta_hal",
  "$WIFI_ROOT_DIR/services/wifi_standard/wifi_hal/wifi_hal_module/wpa_supplicant_hal/wpa_p2p_hal",
  "$WIFI_ROOT_DIR/services/wifi_standard/include",
]

if (defined(ohos_lite)) {
  executable("wifi_hal_service") {
    sources = local_base_sources

    include_dirs = local_base_include_dirs
    include_dirs += [
      "//base/hiviewdfx/hilog_lite/interfaces/native/innerkits",
      "//third_party/bounds_checking_function/include",
      "//third_party/wpa_supplicant/wpa_supplicant-2.9/src",
    ]

    deps = [
      "$WIFI_ROOT_DIR/base:wifi_base_without_crpc",
      "$WIFI_ROOT_DIR/base/cRPC:crpc_server",
      "$WIFI_ROOT_DIR/services/wifi_standard/wifi_hal/etc/init:etc",
      "//base/hiviewdfx/hilog_lite/frameworks/featured:hilog_shared",
      "//third_party/wpa_supplicant/wpa_supplicant-2.9:wpa_supplicant",
    ]

    external_deps = [ "bounds_checking_function:libsec_shared" ]

    cflags_cc = [ "-fno-rtti" ]
    defines = [
      "_GNU_SOURCE",
      "OHOS_ARCH_LITE",
      "AP_INTF=\"$wifi_feature_with_ap_intf\"",
      "AP_NUM=$wifi_feature_with_ap_num",
    ]
    ldflags = [ "-lwpa_client" ]
  }
} else {
  ohos_executable("wifi_hal_service") {
    branch_protector_ret = "pac_ret"
    sanitize = {
      cfi = true
      boundary_sanitize = true
      cfi_cross_dso = true
      debug = false
    }
    install_enable = true
    sources = local_base_sources

    include_dirs = local_base_include_dirs
    include_dirs +=
        [ "//third_party/wpa_supplicant/wpa_supplicant-2.9_standard/src/" ]

    deps = [
      "$WIFI_ROOT_DIR/base:wifi_base_without_crpc",
      "$WIFI_ROOT_DIR/base/cRPC:crpc_server",
      "$WIFI_ROOT_DIR/services/wifi_standard/wifi_hal/etc/init:etc",
      "//third_party/wpa_supplicant/wpa_supplicant-2.9_standard:wpa_client",
    ]

    external_deps = [
      "c_utils:utils",
      "hdf_core:libhdf_ipc_adapter",
      "hdf_core:libhdi",
      "hdf_core:libpub_utils",
      "hilog:libhilog",
    ]

    defines = [
      "AP_INTF=\"$wifi_feature_with_ap_intf\"",
      "AP_NUM=$wifi_feature_with_ap_num",
    ]

    if (wifi_feature_with_local_random_mac) {
      defines += [ "SUPPORT_LOCAL_RANDOM_MAC" ]
    }

    if ("${CONFIG_CTRL_IFACE}" == "unix") {
      defines += [ "WPA_CTRL_IFACE_UNIX" ]
    }

    if (wifi_feature_non_seperate_p2p) {
      defines += [ "NON_SEPERATE_P2P" ]
    }

    cflags_cc = [ "-fno-rtti" ]
    cflags = [ "-Wno-deprecated-non-prototype" ]
    install_images = [ "system" ]
    part_name = "wifi"
    subsystem_name = "communication"
  }

  ohos_executable("wifi_hal_service_updater") {
    branch_protector_ret = "pac_ret"
    sanitize = {
      cfi = true
      boundary_sanitize = true
      cfi_cross_dso = true
      debug = false
    }
    install_enable = true
    sources = local_base_sources

    include_dirs = local_base_include_dirs
    include_dirs +=
        [ "//third_party/wpa_supplicant/wpa_supplicant-2.9_standard/src/" ]

    symlink_target_name = [ "wifi_hal_service" ]
    deps = [
      "$WIFI_ROOT_DIR/base:wifi_base_without_crpc",
      "$WIFI_ROOT_DIR/base/cRPC:crpc_server",
      "$WIFI_ROOT_DIR/services/wifi_standard/wifi_hal/etc/init:etc",
      "//third_party/wpa_supplicant/wpa_supplicant-2.9_standard:wpa_client_updater",
    ]

    external_deps = [
      "c_utils:utils",
      "hdf_core:libhdf_ipc_adapter",
      "hdf_core:libhdi",
      "hdf_core:libpub_utils",
      "hilog:libhilog",
    ]

    defines = [
      "AP_INTF=\"$wifi_feature_with_ap_intf\"",
      "AP_NUM=$wifi_feature_with_ap_num",
    ]

    if (wifi_feature_with_local_random_mac) {
      defines += [ "SUPPORT_LOCAL_RANDOM_MAC" ]
    }

    if ("${CONFIG_CTRL_IFACE}" == "unix") {
      defines += [ "WPA_CTRL_IFACE_UNIX" ]
    }

    if (wifi_feature_non_seperate_p2p) {
      defines += [ "NON_SEPERATE_P2P" ]
    }
    defines += [ "OHOS_EUPDATER" ]
    cflags_cc = [ "-fno-rtti" ]
    cflags = [ "-Wno-deprecated-non-prototype" ]
    install_images = [ "updater" ]
    part_name = "wifi"
    subsystem_name = "communication"
  }
}

group("wifi_hal") {
  deps = [ ":wifi_hal_service" ]
  if (!defined(ohos_lite)) {
    deps += [
      ":wifi_hal_service_updater",
      "$WIFI_ROOT_DIR/base/cRPC:crpc_client_updater",
    ]
  }
}
